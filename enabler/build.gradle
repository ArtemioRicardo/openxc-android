apply plugin: 'com.android.application'
apply plugin: 'bugsnag'
apply plugin: 'com.github.triplet.play'

def versionMajor = 6
def versionMinor = 1
def versionPatch = 0

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "com.openxcplatform.enabler"
        minSdkVersion 10
        targetSdkVersion 22
        versionCode 600100
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
    }

   signingConfigs {
        debug {
            storeFile rootProject.file('keys/debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }

        release {
            storeFile rootProject.file('keys/release.keystore')
            storePassword System.getenv("KEYSTORE_PASS")
            keyAlias System.getenv("ALIAS_NAME")
            keyPassword System.getenv("ALIAS_PASS")
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            minifyEnabled false
            testCoverageEnabled = true
            buildConfigField "String", "BUGSNAG_TOKEN", "null"
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                getDefaultProguardFile('proguard-android-optimize.txt'),
                'proguard-rules.txt'
            buildConfigField "String", "BUGSNAG_TOKEN",
                System.getenv("BUGSNAG_TOKEN") ?: "null"
        }
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
      exclude 'LICENSE.txt'
    }

    if (System.getenv("TRAVIS")) {
        bugsnag {
            apiKey System.getenv("BUGSNAG_TOKEN")
        }

        play {
            serviceAccountEmail = System.getenv("SERVICE_EMAIL")
            pk12File = rootProject.file('keys/play-store.p12')
            track = 'beta'
        }
    }
}

dependencies {
    compile project(":library")

    compile 'com.android.support:support-v4:19.1.0'
    compile 'com.bugsnag:bugsnag-android:3.2.2'

    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
    androidTestCompile 'org.mockito:mockito-core:1.10.19'
    androidTestCompile 'com.google.dexmaker:dexmaker:1.2'
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2'
}

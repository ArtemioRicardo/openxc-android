apply plugin: 'android-library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

version = "6.1.0"
group = "com.openxcplatform"

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 22
        versionCode 2
        versionName version
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile 'com.google.code.gson:gson:2.3'
    compile 'com.google.guava:guava:18.0'
    compile 'com.google.protobuf:protobuf-java:2.6.1'
    compile 'commons-io:commons-io:2.4'
    compile 'com.android.support:support-v4:19.1.0'

    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.robolectric:robolectric:3.0-rc3'
    testCompile 'org.robolectric:shadows-httpclient:3.0-rc3'
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                name 'OpenXC Android Library'
                packaging 'aar'
                description 'OpenXC vehicle data library for Android applications'
                url 'https://github.com/openxc/openxc-android'

                scm {
                    url 'scm:git@github.com:openxc/openxc-android.git'
                    connection 'scm:git@github.com:openxc/openxc-android.git'
                    developerConnection 'scm:git@github.com:openxc/openxc-android.git'
                }

                licenses {
                    license {
                        name 'BSD License'
                        url 'http://opensource.org/licenses/BSD-2-Clause'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'peplin'
                        name 'Chris Peplin'
                        email 'chris.peplin@gmail.com'
                    }
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}
